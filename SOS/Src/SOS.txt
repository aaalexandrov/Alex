- Lexical closures
- Non-constant upvalues
- Co-routines
- Iteration
- Number of elements in table
- Struct-type table element accessors (.field)
- Compiled fragment saving / loading
- Compiled code optimizer

- String length/element access operations
- For loops
- Tail function calls

- Garbage collection
- Script to native code calls
- Native code to script calls
- Error reporting & recovery
- Grammar optimizer

- Automatic function execution parallelism


+ Non-stack based instruction set
+ Multiple return value rules for expressions
+ Real local variables / scoping
+ nil value
+ String concatenation operator


-------------------------------------------------------------------------------

Constant ::= "nil" | "true" | "false" | String | Number | Table
Variable ::= Identifier
ParamList ::= "(" [ ExpressionList ] ")"
Call ::= ( Variable { Index } | FunctionDef ) ParamList { { Index } ParamList }
Indexable ::= Call | Variable | "(" Expression ")" 
Index ::= "[" Expression "]"
Operand ::= Constant | ( Indexable { Index } )
Power ::= Operand [ "^" Power ]
Mult ::= Power { ( "*" | "/" ) Power }
Sum ::= ["+" | "-"] Mult { ( "+" | "-" ) Mult }
Concat ::= Sum { ".." Sum }

Comparison ::= Concat [ ("==" | "~=" | "<" | ">" | "<=" | ">=") Concat ]
Not ::= { "not" } Comparison
And ::= Not { "and" Not }
Or ::= And { "or" And }

TableKey = Identifier | Constant
TableValue = [ TableKey "=" ] Expression
Table ::= "{" [ TableValue { "," TableValue } [ "," ] ] "}"

IdentifierList ::= Identifier { "," Identifier }
FunctionDef ::= "function" "(" [ IdentifierList ] ")" { Operator } "end"
Expression ::= Or | FunctionDef
ExpressionList ::= Expression { "," Expression }

Locals ::= "local" IdentifierList [ "=" ExpressionList ]
LValue ::= ( Indexable Index { Index } ) | Variable
Assignment ::= LValue { "," LValue } "=" ExpressionList

Return ::= "return" [ ExpressionList ]
If ::= "if" Expression "then" { Operator } [ "else" { Operator } ] "end"
While ::= "while" Expression "do" { Operator } "end"

Operator ::= While | If | Return | Locals | Assignment | Call
