- Non-stack based instruction set
- Real local variables / scoping
- Lexical closures
- Non-constant upvalues
- Co-routines
- Iteration
- Number of elements in table
- Compiled fragment saving / loading
- Compiled code optimizer

- String concatenation operator
- nil value
- For loops
- Multiple return value rules for expressions

- Garbage collection
- Script to native code calls
- Native code to script calls
- Grammar optimizer

- Automatic function execution parallelism

-------------------------------------------------------------------------------

Constant ::= "true" | "false" | String | Number | Table
Variable ::= Identifier
Call ::= ( Variable | FunctionDef ) "(" [ ExpressionList ] ")" { "(" [ ExpressionList ] ")" }
Indexable ::= Call | Variable | "(" Expression ")" 
Index ::= "[" Expression "]"
Operand ::= Constant | ( Indexable { Index } )
Power ::= Operand [ "^" Power ]
Mult ::= Power { ( "*" | "/" ) Power }
Sum ::= ["+" | "-"] Mult { ( "+" | "-" ) Mult }

Comparison ::= Sum [ ("==" | "~=" | "<" | ">" | "<=" | ">=") Sum ]
Not ::= [ "not" ] Comparison
And ::= Not { "and" Not }
Or ::= And { "or" And }

TableKey = Identifier | Constant
TableValue = [ TableKey "=" ] Expression
Table ::= "{" [ TableValue { "," TableValue } [ "," ] ] "}"

FunctionDef ::= "function" "(" [ Identifier { "," Identifier } ] ")" { Operator } "end"
Expression ::= Or | FunctionDef
ExpressionList ::= Expression { "," Expression }

LValue ::= ( Indexable Index { Index } ) | Variable
Assignment ::= LValue { "," LValue } "=" ExpressionList

Return ::= "return" [ ExpressionList ]
If ::= "if" Expression "then" { Operator } [ "else" { Operator } ] "end"
While ::= "while" Expression "do" { Operator } "end"

Operator ::= If | Return | Assignment | Call
